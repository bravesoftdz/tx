[[@txDefs,SQLiteData,txSession,txFilter,txFilterSql,]][[!var
i,id,rid,obj2id,oldRefTypeID,newRefTypeID,oldWeight,newWeight1,newWeight,RlmID:integer;
url:string;
desc:WideString;
filter,fs:string;
f:TtxFilter;
fq:TtxSqlQueryFragments;
qr:TSQLiteStatement;
ids:TIdList;
]][[

//TODO: transaction?

id:=Context['id'].AsInteger;
rid:=Context['rid'].AsInteger;
obj2id:=Context['obj2'].AsInteger;
newRefTypeID:=Context['reftype'].AsInteger;
newWeight1:=Context['weight'].AsInteger;
desc:=Context['desc'].Value;
filter:=Context['filter'].Value;
CheckFormProtect(Context);

ids:=TIdList.Create;
try
  if (filter='') then ids.Add(id) else
   begin
    f:=TtxFilter.Create;
    fq:=TtxSqlQueryFragments.Create(itObj);
    try
      fq.Fields:='Obj.id';
      f.FilterExpression:=filter;
      if f.ParseError<>'' then begin<<p style="color:red;">>=f.ParseError<</p>>end;
      fq.AddFilter(f);
      fq.Where:='Obj.rlm_id'+Session.Realms[rpView].SQL+' AND ('+fq.Where+')';
      fs:=fq.SQL;
    finally
      f.Free;
      fq.Free;
    end;

    qr:=TSQLiteStatement.Create(Session.DbCon,fs);
    try
      while qr.Read and Context.Connected do ids.Add(qr.GetInt('id'));
    finally
      qr.Free;
    end;

   end;

  for i:=0 to ids.Count-1 do
   begin
    id:=ids[i];
    if filter<>'' then rid:=0;

    //check realm
    if rid=0 then
     begin
      RlmID:=DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[id],Session.RealmNew);
      oldRefTypeID:=0;
      oldWeight:=0;
     end
    else
     begin
      qr:=TSQLiteStatement.Create(Session.DbCon,'SELECT Obj.rlm_id, Ref.reftype_id, Ref.weight FROM Ref INNER JOIN Obj ON Obj.id=Ref.obj1_id WHERE Ref.id=?',[rid]);
      try
        if qr.Read then
         begin
          RlmID:=qr.GetInt('rlm_id');
          oldRefTypeID:=qr.GetInt('reftype_id');
          oldWeight:=qr.GetInt('weight');
         end
        else
         begin
          RlmID:=Session.RealmNew;
          oldRefTypeID:=0;
          oldWeight:=0;
         end;
      finally
        qr.Free;
      end;
     end;
    Session.HasRealmPermission(rid,RlmID,rpEdit);

    //realm on obj2 also?

    //TODO: 'default': ???

    newWeight:=newWeight1;
    if (rid<>0) and (newRefTypeID<>oldRefTypeID) then
      newWeight:=newWeight-DBSingleValue('SELECT weight FROM RefType WHERE id=?',[oldRefTypeID],0);
    if (rid=0) or (newRefTypeID<>oldRefTypeID) then
      newWeight:=newWeight+DBSingleValue('SELECT weight FROM RefType WHERE id=?',[newRefTypeID],0);

    Session.DbCon.Execute('BEGIN TRANSACTION');
    try

        if rid=0 then
          Session.DbCon.Execute('INSERT INTO Ref (obj1_id,obj2_id,reftype_id,desc,weight,c_ts,c_uid,m_ts,m_uid) VALUES (?,?,?,?,?,?,?,?,?)',
            [id,obj2id,newRefTypeID,desc,newWeight,VNow,Session.UserID,VNow,Session.UserID],rid)
        else
          //move references not supported
          Session.DbCon.Execute('UPDATE Ref SET obj2_id=?, reftype_id=?, desc=?, weight=?, m_ts=?, m_uid=? WHERE id=?',
            [obj2id,newRefTypeID,desc,newWeight,VNow,Session.UserID,rid]);

        if newWeight<>oldWeight then Session.DbCon.Execute('UPDATE Obj SET weight=weight+(?) WHERE id=?',[newWeight-oldWeight,id]);

        //report
        Session.DbCon.Execute('INSERT INTO Rpt (obj_id,ts,uid,desc,weight,reftype_id,obj2_id) VALUES (?,?,?,?,?,?,?)',
          [id,VNow,Session.UserID,desc,newWeight,newRefTypeId,obj2id]);

        TermStore.StoreTerms(itRef,rid,id,desc);
        if Use_ObjTokRefCache then Context.Include('aObjTokRefCache.xxmi',[id]);

        if Use_Unread and not Session.Stealth then
          Session.DbCon.Execute('INSERT INTO Obx (obj_id) VALUES (?)',[id]);

        Session.DbCon.Execute('COMMIT TRANSACTION');
    except
        Session.DbCon.Execute('ROLLBACK TRANSACTION');
        raise;
    end;

   end;
  if filter='' then url:='Item.xxm?x=i'+IntToStr(id) else url:='Modify.xxm?x=99&filter='+URLEncode(filter)+'&c='+IntToStr(ids.Count);
finally
  ids.Free;
end;
Session.AddFilterRecent(itObj,obj2id);
Session.AddFilterRecent(itRefType,newRefTypeID);
Session.UpdateID:=id;

Context.Redirect(url,true);
<<a href="[[=url]]">>=url<</a>
