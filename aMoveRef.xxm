[[@txDefs,DataLank,txSession,]][[!var
x:string;
ItemType:TtxItemType;
ItemID,newPid,aid,RlmID,obj2id,newRefTypeID,newWeight,rid:integer;
desc:WideString;
url:string;
]][[

//TODO: transaction?
//TODO: Session.UpdateID to move node

CheckFormProtect(Context);

x:=Context['x'].Value;
txItem(x,ItemType,ItemID);
newPid:=Context['parent'].AsInteger;

obj2id:=Context['pid'].AsInteger;
newRefTypeID:=Context['reftype'].AsInteger;
desc:=Context['desc'].Value;

//check not creating invalid path
aid:=newPid;
while (aid<>0) and (aid<>ItemID) do
  aid:=DBSingleValue(txItemSQL_PidById[ItemType],[aid],0);
if aid<>0 then raise Exception.Create('Can''t move item to a branch under itself.');

if ItemType<>itObj then raise Exception.Create('MoveRef not allowed on non-Obj');

//check realm
RlmID:=DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[ItemID],DefaultRlmID);
Session.HasRealmPermission(ItemID,RlmID,rpEdit);
if newPid=0 then RlmID:=0 else RlmID:=DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[newPid],DefaultRlmID);
Session.HasRealmPermission(newPid,RlmID,rpEdit);
//TODO: else RealmPermission(rpEdit,0)?

//realm on obj2 also?

//TODO: 'default': ???

newWeight:=Context['weight'].AsInteger+DBSingleValue('SELECT weight FROM RefType WHERE id=?',[newRefTypeID],0);

Session.DbCon.Execute('BEGIN TRANSACTION');
try
	//perform doesn't set c_ts, c_uid
	Session.DbCon.Execute(txItemSQL_Move[ItemType],[newPid,ItemID]);


	Session.DbCon.Execute('INSERT INTO Ref (obj1_id,obj2_id,reftype_id,desc,weight,c_ts,c_uid,m_ts,m_uid) VALUES (?,?,?,?,?,?,?,?,?)',
	  [ItemID,obj2id,newRefTypeID,desc,newWeight,VNow,Session.UserID,VNow,Session.UserID],rid);
	if newWeight<>0 then Session.DbCon.Execute('UPDATE Obj SET weight=weight+(?) WHERE id=?',[newWeight,ItemID]);

	//report
	Session.DbCon.Execute('INSERT INTO Rpt (obj_id,ts,uid,desc,weight,reftype_id,obj2_id) VALUES (?,?,?,?,?,?,?)',
	  [ItemID,VNow,Session.UserID,desc,newWeight,newRefTypeID,obj2id]);

	TermStore.StoreTerms(itRef,rid,ItemID,desc);
	if Use_ObjTokRefCache then Context.Include('aObjTokRefCache.xxmi',[ItemID]);

	Session.DbCon.Execute('COMMIT TRANSACTION');
except
	Session.DbCon.Execute('ROLLBACK TRANSACTION');
	raise;
end;

Session.AddFilterRecent(itObj,newPid);
Session.AddFilterRecent(itRefType,newRefTypeID);
Session.UpdateID:=ItemID;

url:='Item.xxm?x='+x;
Context.Redirect(url,true);
<<a href="[[=url]]">>=url<</a>
